// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Parses and runs binary WebAssembly spec tests (.bin.wast).
def OUT = Trace.OUT;
component spectest {
	def expected = Strings.newMap<bool>();
	def trace = TraceBuilder.new();
	var skip_actions = false;

	def main(args: Array<string>) -> int {
		var count = 0;
		var engine = Engine.new();
		filterOptions(args, TraceOptions.parse);
		filterOptions(args, MetricOptions.parse);
		filterOptions(args, EngineOptions.parse(engine, _));
		for (i < args.length) {
			var a = args[i];
			if (a == null) continue;
			if (Strings.equal(a, "--skip-actions")) {
				skip_actions = true;
				args[i] = null;
				continue;
			}
			if (Strings.startsWith(a, "-expected=")) {
				TestUtil.loadExpectedFile(expected, Arrays.range(a, "-expected=".length, a.length));
				continue;
			}
			if (Strings.endsWith(a, ".wast")) count++;
		}
		if (count == 0) {
			System.puts("spectest: no input files\n");
			return 3;
		}
		Execute.reset();
		if (count > 1) {
			OUT.puts("##>");
			OUT.putd(count);
			OUT.ln();
		}
		for (a in args) {
			if (a == null) continue;
			if (Strings.endsWith(a, ".wast")) runTest(engine, a);
		}

		Metrics.report();
		return 0;
	}
	def runTest(engine: Engine, filename: string) {
		OUT.puts("##+");
		OUT.puts(filename);
		OUT.outln();
		var input = System.fileLoad(filename);
		if (input == null) {
			OUT.puts("##-fail: could not load file").outln();
			return;
		}
		var p = SpecTestParser.new(filename, input, makeExternRef(Vector.new(), _));
		p.parseFile();
		if (p.ok) {
			var ip = SpecTestInterpreter.new(engine, filename);
			ip.skip_actions = skip_actions;
			ip.ignore_failure = expected[filename];
			if (ip.run(p.commands)) OUT.puts("##-ok").outln();
		} else if (expected[filename]) {
			OUT.put3("##-ok: (ignored expected failure: @ %d:%d %s)", p.error_loc.0, p.error_loc.1, p.error_msg).outln();
		} else {
			OUT.put1("##-fail: %s ", filename);
			OUT.put3("%d:%d %s", p.error_loc.0, p.error_loc.1, p.error_msg).outln();
		}
	}
	def makeExternRef(cache: Vector<HostObject>, i: int) -> HostObject {
		if (i >= cache.length || cache[i] == null) {
			cache.set(i, NumberedObject.new(i));
		}
		return cache[i];
	}
	def renderBinaryModule(data: Array<byte>, pos: int, length: int) {
		OUT.puts("(module binary");
		for (i < length) {
			if (i % 16 == 0) {
				if (i > 0) OUT.puts("\"");
				OUT.puts("\n  \"");
			}
			OUT.put1("\\%x", data[pos + i]);
		}
		OUT.puts("\")").outln();
	}
	def filterOptions(args: Array<string>, func: string -> bool) {
		for (i < args.length) {
			var a = args[i];
			if (a != null && func(a)) args[i] = null;
		}
	}
}
class NumberedObject(num: int) extends HostObject {
	def render(buf: StringBuilder) -> StringBuilder {
		return buf.put1("#%d", num);
	}
}