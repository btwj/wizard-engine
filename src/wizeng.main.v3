// Copyright 2021 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Parses and runs binary .wasm files.
def main(args: Array<string>) -> int {
	var engine = Engine.new();
	var programArgs: Array<string>;
	var path: string;
	// Parse option and extract module and module arguments
	for (i < args.length) {
		var a = args[i];
		if (a.length > 0 && a[0] == '-') {
			var ok = TraceOptions.parse(a)
				|| EngineOptions.parse(engine, a)
				|| WizengOptions.parse(engine, a)
				|| MonitorOptions.parse(a)
				|| MetricOptions.parse(a);

			if (!ok) return ErrorBuilder.new()
				.puts("wizeng: unrecognized option ")
				.putsq(a)
				.ln()
				.exit(1);
		} else {
			path = a;
			programArgs = Arrays.range(args, i, args.length);
			break;
		}
	}

	if (WizengOptions.genintFile != null) {
		var err = Target.genInterpreterIntoFile(WizengOptions.genintFile);
		if (err != null) return err.exit(2);
		if (path == null) return 0; // all done
	}

	if (WizengOptions.help) {
		printHelp();
		return 0;
	}

	if (WizengOptions.version) {
		printVersion();
		return 0;
	}

	if (path == null) return ErrorBuilder.new()
		.puts("wizeng: no input files\n")
		.exit(3);

	if (!Strings.endsWith(path, ".wasm")) return ErrorBuilder.new()
		.puts(path)
		.puts(": unknown file format, expected .wasm\n")
		.exit(4);

	// Parse the binary module
	var monitors = MonitorOptions.getMonitors();
	if (monitors != null) Execute.tiering.onMonitorsStart();
	var result = engine.loadWasmFile(path);
	var module: Module;
	match (result) {
		Ok(m) => module = m;
		FileNotFound => return ErrorBuilder.new()
			.puts("wizeng: could not load file ")
			.puts(path)
			.ln()
			.exit(2);
		ParseError(code, path, section, error_index, error_pos, error_msg) => return ErrorBuilder.new()
			.render(code, path, section, error_index, error_pos, error_msg)
			.exit(3);
	}
	// Call monitors for parsed module
	for (l = monitors; l != null; l = l.tail) l.head.onParse(module);
	var err = ErrorGen.new(path);
	if (monitors != null) Execute.tiering.onMonitorsFinish(module, err);

	// Gather the list of import processors
	var imports = Array<Exportable>.new(module.imports.length);
	var i = Instantiator.new(engine.extensions, module, imports, err);
	var map = Strings.newMap<ImportProcessor>();

	for (j < module.imports.length) {
		var name = module.imports[j].imp.module_name;
		if (map[name] == null) {
			var p = HostModuleRegistry.map[name];
			if (p == null) return ErrorBuilder.new()
				.puts(path)
				.put2(": import[%d] from unknown module \"%s\"\n", j, name)
				.exit(7);
			var trace = Trace.moduleFilter != null && Trace.moduleFilter.matchesStr(name, null);
			p.init(programArgs, trace);
			map[name] = p;
			i.processors.put(p);
		}
	}

	// Reset global execution state.
	Execute.reset();

	// Add tracing to import calls as specified by filters, if any.
	if (Trace.moduleFilter != null) i.binder = ImportTracer.new(Trace.moduleFilter).wrap;

	// Run the instantiatior.
	var instance = i.run();
	if (!err.ok()) return ErrorBuilder.new()
		.render(err.error_code, path, err.section, err.error_index, err.error_pos, err.error_msg)
		.exit(6);

	// Call monitors on finish
	for (l = monitors; l != null; l = l.tail) l.head.onInstantiate(instance);

	// Try to find the main entrypoint.
	var entry: (Function, Array<Value>);
	for (j < i.processors.length) {
		var p = i.processors[j];
		if (HostModule.?(p)) {
			entry = HostModule.!(p).getEntry(instance);
			if (entry.0 != null) break;
		}
	}

	if (entry.0 == null) {
		entry = findDefaultMain(instance);
	}

	if (entry.0 == null) return ErrorBuilder.new()
		.puts(path)
		.puts(": no main export from module\n")
		.exit(7);

	// Execute the start function, if any.
	if (module.start_function >= 0) {
		var start = instance.functions[module.start_function];
		for (l = monitors; l != null; l = l.tail) l.head.onStart(start);
		var r = Execute.call(start, Values.NONE);
		match (r) {
			Throw(thrown) => return exitTrap(monitors, instance, thrown);
			_ => ; // do nothing
		}
	}

	// Execute the main entrypoint.
	for (l = monitors; l != null; l = l.tail) l.head.onMain(entry.0, entry.1);
	var r = Execute.call(entry.0, entry.1);
	match (r) {
		Break => ;
		Value(vals) => {
			// Call monitors on finish
			for (l = monitors; l != null; l = l.tail) l.head.onFinish(instance);
			if (vals.length != 1) return 0;
			match (vals[0]) {
				I32(val) => return int.view(val);
				I64(val) => return int.view(val);
				_ => return 0;
			}
		}
		Throw(thrown) => {
			return exitTrap(monitors, instance, thrown);
		}
	}
	// Call monitors on finish
	for (l = monitors; l != null; l = l.tail) l.head.onFinish(instance);
	Metrics.report();
	return 0;
}
def exitTrap(monitors: List<Monitor>, instance: Instance, thrown: Throwable) -> int {
	if (Trap.?(thrown) && Trap.!(thrown).reason == TrapReason.EXIT) {
		// Call monitors on exit
		for (l = monitors; l != null; l = l.tail) l.head.onExit(Execute.exit_code);
		Metrics.report();
		return Execute.exit_code;
	} else {
		var e = ErrorBuilder.new();
		var indent = 0;
		for (s = thrown.stacktrace; s != null; s = s.prev) {
			for (t in s.frames) {
				for (i < indent) e.puts("  ");
				t.0.render(e);
				e.puts(" +");
				e.putd(t.1);
				e.ln();
				indent++;
			}
			if (s.host != null) {
				for (i < indent) e.puts("  ");
				e.puts("\t@ ");
				s.host.render(e);
				e.ln();
				indent++;
			}
		}
		for (i < indent) e.puts("  ");
		e.puts("!");
		thrown.render(e);
		e.ln();
		// Call monitors on finish
		for (l = monitors; l != null; l = l.tail) l.head.onFinish(instance);
		Metrics.report();
		return e.exit(10);
	}
}

def printHelp() {
	putWizeng(Trace.OUT).ln();
	Trace.OUT.puts("Usage: wizeng [options] <wasm files>\n\n");
	EngineOptions.printHelp();
	TraceOptions.printHelp();
}

def printVersion() {
	putWizeng(Trace.OUT).ln();
	if (Version.buildData != null) Trace.OUT.puts("Build data: ").puts(Version.buildData).ln();
	Trace.OUT.out(Trace.STDOUT);
}

def putWizeng(out: StringBuilder) -> StringBuilder {
	return out.puts("Wizard Engine ").puts(Version.version);
}

// Host modules must be registered by their name so the Wizard engine can find them.
component HostModuleRegistry {
	def map = Strings.newMap<HostModule>();
	def add(m: HostModule) {
		map[m.name] = m;
	}
}
// A host module (with state) that can provide imports to a module as well as the entrypoint and exit value.
class HostModule extends ImportProcessor {
	new(name: string) super(name) { }

	// Initializes the host module with the command-line arguments.
	def init(args: Array<string>, trace: bool) { }
	// Get the main export and arguments, if exported from the instance.
	def getEntry(instance: Instance) -> (Function, Array<Value>) { return (null, null); }
	// De-initialize after running.
	def deinit(result: Result) { }
}
// Wraps imported functions with tracing. Can apply a filter to be selective on which imports to trace.
class ImportTracer(var filter: DeclFilter) {
	def wrap(decl: Decl, ex: Exportable) -> Exportable {
		if (!Function.?(ex)) return ex;
		var func = Function.!(ex);
		var imp = decl.imp;
		if (!filter.matchesStr(imp.module_name, imp.field_name)) return ex;
		var name = Strings.format1("trace(%q)", func.render);
		func = HostFunctionN.new(name, func.sig, traceCall(imp, func, _));
		return func;
	}
	def traceCall(imp: ImportInfo, f: Function, args: Array<Value>) -> HostResult {
		var out = Trace.OUT;
		out.puts(imp.module_name);
		out.putc('.');
		out.puts(imp.field_name);
		Values.renderVals(out, args);
		out.outln();
		if (WasmFunction.?(f)) {
			var result = Execute.call(f, args);
			out.puts("  ==> ");
			result.render(out);
			out.outln();
			match (result) {
				Break => return HostResult.Throw(Trap.new(TrapReason.ERROR, "unexpected break", null));
				Value(vals) => return HostResult.ValueN(vals);
				Throw(thrown) => return HostResult.Throw(thrown);
			}
		}
		var result: HostResult;
		match (f) {
			hf: HostFunction0 => {
				if (Trace.interpreter) Execute.traceCallHostFunction(hf);
				result = hf.invoke0();
			}
			hf: HostFunction1 => {
				if (Trace.interpreter) Execute.traceCallHostFunction(hf);
				result = hf.invoke1(args[0]);
			}
			hf: HostFunction2 => {
				if (Trace.interpreter) Execute.traceCallHostFunction(hf);
				result = hf.invoke2(args[0], args[1]);
			}
			hf: HostFunction3 => {
				if (Trace.interpreter) Execute.traceCallHostFunction(hf);
				result = hf.invoke3(args[0], args[1], args[2]);
			}
			hf: HostFunctionN => {
				if (Trace.interpreter) Execute.traceCallHostFunction(hf);
				result = hf.invokeN(args);
			}
		}
		out.puts("  ==> ");
		match (result) {
			Value0 => {
			}
			Value1(val) => {
				val.render(out);
			}
			ValueN(vals) => {
				if (vals.length == 1) {
					vals[0].render(out);
				} else {
					Values.renderVals(out, vals);
				}
			}
			Throw(thrown) => thrown.render(out);
			TailCall(func, args) => out.puts("<tailcall>");
		}
		out.outln();
		return result;
	}
}
// XXX: wizeng module
// .force_gc()
// .throw()
// .crash()
// .compile_module()

// Options specific to the "wizeng" executable.
component WizengOptions {
	var help = false;
	var version = false;
	var genintFile: string; // file to patch interpreter code into

	def parse(engine: Engine, arg: string) -> bool {
		if (Strings.startsWith(arg, "-gen-int=")) {
			genintFile = Arrays.range(arg, 9, arg.length);
			return true;
		}
		if (Strings.equal(arg, "-help")) {
			help = true;
			return true;
		}
		if (Strings.equal(arg, "-version")) {
			version = true;
			return true;
		}
		return false;
	}
}
def findDefaultMain(instance: Instance) -> (Function, Array<Value>) {
	var module = instance.module;
	var exports = module.exports;
	var found: Function;
	for (j < exports.length) {
		var mex = module.exports[j], iex = instance.exports[j];
		if (!Function.?(iex)) continue;
		found = Function.!(iex);
		if (Strings.equal("main", mex.0)) {
			return (found, Arrays.map(found.sig.params, Values.default));
		}
	}
	return (null, null);
}